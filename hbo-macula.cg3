DELIMITERS = "<svb>" ;

####################
# Boundaries
####################

LIST PB = pb ;
LIST CB = cb ;
LIST SB = sb ;
LIST SVB = svb ;
LIST BD = pb cb sb svb ;

####################
# POS
####################

LIST Det = art ;
LIST Pr = prep ;
LIST Noun = subs nmpr (verb ptcp) (verb ptca) ;
LIST RelMark = ("אשׁר" conj @mark) ("ה" conj CP Rela) ("שׁ" conj) ;
LIST PRON = prde prps prn ;
LIST ADV = advb nega ;
LIST SCONJ = "כי" "פן" "אשׁר" "אם" "שׁ" "ה" "לולא" "לו" (conj retag:prep) (conj retag:subs ppre) ;

####################
# Subcategories
####################

LIST Participle = ptca ptcp ;

####################
# Text-Fabric labels
####################

# Complete

LIST Function = Adju Cmpl Conj EPPr ExsS Exst Frnt Intj IntS Loca Modi ModS NCop NCoS Nega Objc PrAd PrcS PreC Pred PreO PreS PtcO Ques Rela Subj Supp Time Unkn Voct ;
LIST ClauseType = AjCl CPen Defc Ellp InfA InfC MSyn NmCl Ptcp Reop Unkn Voct Way0 WayX WIm0 WImX WQt0 WQtX WxI0 WXIm WxIX WxQ0 WXQt WxQX WxY0 WXYq WxYX WYq0 WYqX xIm0 XImp xImX XPos xQt0 XQtl xQtX xYq0 XYqt xYqX ZIm0 ZImX ZQt0 ZQtX ZYq0 ZYqX ; # feature typ
LIST AGen = m f ;

####################
# Helper labels
####################

LIST NQ = NQ ;
LIST QuoteLevel = NQ Q QQ ;

####################
# Word lists
####################

LIST ConjAblePrep = "כמו" "למען" "עד" ;
LIST ConjAbleSubsPrep = "בלת" ;
LIST SubsToPrep = "אחר" "בין" "אצל" "בעד" ;

LIST Masculine = "מכיר" "אשׁר" "בצע" "בקר" ;
# TODO: BDB says sometimes masculine
LIST FeminineMixed = "ארץ" "יד" "צאן" "אשׁ" "גן" "גפן" ;
# TODO: BDB says sometimes feminine
LIST MasculineMixed = "לחם" "דרך" "אור" "אות" "ארון" "גמל" ;

####################
# Universal Dependencies
####################

LIST @root = @root ;     # The root of the sentence, often a finite verb
LIST @nsubj = @nsubj ;   # The nominal subject of the sentence
LIST @nsubj:outer = @nsubj:outer ;
LIST @amod = @amod ;       #
LIST @advmod = @advmod ; # An adverbial modifier
LIST @case = @case ;     # The relation of an adposition to its head
LIST @acl:relcl = @acl:relcl ;       # A clause which modifies a nominal
LIST @nmod = @nmod ;     # Nominal modifier
LIST @dobj = @dobj ;     # The direct object of the sentence
LIST @punct = @punct ;   # Any punctuation
LIST @cop = @cop ;       #
LIST @nmod:poss = @nmod:poss ;
LIST @obl = @obl ;
LIST @obl:npmod = @obl:npmod ;
LIST @obj = @obj ;
LIST @advcl = @advcl ;
LIST @aux = @aux ;
LIST @parataxis = @parataxis ;
LIST @det = @det ;
LIST @csubj = @csubj ;
LIST @nummod = @nummod ;
LIST @compound = @compound ;
LIST @compound:smixut = @compound:smixut ;
LIST @cc = @cc ;
LIST @conj = @conj ;
LIST @ccomp = @ccomp ;
LIST @mark = @mark ;
LIST @discourse = @discourse ;
LIST @vocative = @vocative ;
LIST @appos = @appos ;
LIST @dislocated = @dislocated ;
LIST @xcomp = @xcomp ;
LIST @flat = @flat ;
LIST @flat:name = @flat:name ;
LIST @orphan = @orphan ;
LIST @fixed = @fixed ;
LIST @dep = @dep ;       # Any remaining dependency

####################
# Unsorted lists
####################

LIST NPish = NP PP ;
LIST SubsOverride = (Genesis w20500) (Ruth w1233) (Genesis w13703) (Genesis w14216) ; # actually nouns, don't retag as prep
LIST ItgAdv = "מתי" "איפה" "איך" "אן" "מדוע" ;
LIST VoctNonCROverride = (Genesis s81570) (Genesis s81725) (Genesis s85145) (Genesis s82809) (Genesis w9312) ;
LIST ActuallyInf = (Genesis w13111) ;

########################################
# INDIVIDUAL WORDS
########################################

BEFORE-SECTIONS

####################
# Hide Punctuation
####################

MAP @punct (punct) ;
REMCOHORT IGNORED (punct) ;

####################
# Corrections
####################

LIST MislabeledAsNarrative = w1404 w1405 w1423 w1424 w14251 w14252 w14253 (Genesis s80885) (Ruth s5672) (Ruth s5673) (Genesis s83562) (Ruth s5647) (Ruth s5650) (Ruth s5651) (Genesis s85072) (Genesis s83308) (Genesis s83309) (Genesis s84608) (Genesis s83116) (Genesis s83082) (Genesis s81615) (Genesis s81616) (Genesis s81881) ;
SUBSTITUTE (N) (Q) MislabeledAsNarrative ;

LIST UnknownShouldBeNarrative = (Genesis w2137) ;
SUBSTITUTE (?) (N) UnknownShouldBeNarrative ;

SUBSTITUTE (infc) (infa) (Ruth w797) ;

ADD (m) Noun - AGen IF (0 Masculine OR MasculineMixed) ;
ADD (f) Noun - AGen IF (0 FeminineMixed) ;

####################
# Nested Quotations
####################

LIST DoubleQuoteGen = s80831 s80834 s80836 s80837 s80886 s81424 s81443 s81543 s81737 s81766 s81767 s81785 s81829 s81933 s81999 s82001 s82028 s82037 s82159 s82295 s82315 s82367 s82380 s82382 s82410 s82412 s82413 s82446 s82450 s82599 s82626 s82629 s82776 s82946 s83252 s83255 s83285 s83303 s83318 s83319 s83327 s83431 s83432 s83449 s83459 s83470 s83474 s83479 s83480 s83482 s83510 s83626 s83628 s83906 s83919 s84071 s84283 s84313 s84382 s84435 s84464 s84492 s84537 s84545 s84551 s84552 s84555 s84638 s84673 s84681 s84708 s84709 s84719 s84722 s84765 s84804 s84805 s84828 s84934 s84941 s84943 s85134 s85229 s85230 s85261 s85262
c29820 ; # TODO: handle rela:Objc properly
LIST DoubleQuoteRuth = s5648 s5649 s5720 s5825 ;
SET DoubleQuote = (Genesis) + DoubleQuoteGen OR (Ruth) + DoubleQuoteRuth ;
SUBSTITUTE (Q) (QQ) DoubleQuote ;

ADD NQ (*) - QuoteLevel ;

####################
# MWE names
####################

SUBSTITUTE (nmpr a) (subs c m retag:nmpr) ("בית" wp1) ;
SUBSTITUTE (nmpr a) (subs c m retag:nmpr) ("בן" wp1) ;
SUBSTITUTE (nmpr) (subs retag:nmpr m) ("אל" wp2) ;
SUBSTITUTE (nmpr) (subs retag:nmpr) ("לחם" wp2) ;
SUBSTITUTE (nmpr) (subs retag:nmpr) ("צדק" wp2) ;
SUBSTITUTE ("מלכי" nmpr) ("מלך" subs c retag:nmpr) (wp1 Genesis w6557) ;
SUBSTITUTE ("עמי" nmpr) ("עם" subs retag:nmpr has_prn) ("עמי" wp2) ;
SUBSTITUTE ("אוני" nmpr) ("און" subs retag:nmpr has_prn) ("אוני" wp2) ;
ADDCOHORT ("<prn>" "אני" prn p1 sg) AFTER ("עם" subs retag:nmpr) ;
ADDCOHORT ("<prn>" "אני" prn p1 sg) AFTER ("און" subs retag:nmpr) ;

MAP @flat:name (nmpr wp2) IF (-1 (nmpr wp1)) ;
SETPARENT @flat:name TO (-1* (nmpr wp1)) ;

SETPARENT (wp2) TO (-1 (wp1)) ;
SETPARENT (*) TO (p (wp2) LINK p (wp1)) ;
MAP @compound:smixut (wp2) IF (p (wp1 c)) ;

####################
# MWE conjunctions
####################

SETPARENT ("אם" conj) OR ("כי" conj) TO (-1 ("עד" prep)) ;
MAP @fixed ("אם" conj) OR ("כי" conj) IF (-1 ("עד" prep)) ;
SUBSTITUTE (prep) (conj retag:prep) ("עד") IF (c @fixed) ;

####################
# Retagging
####################

# quotation
MERGECOHORTS ("<le'emor>" "$2$1"v SCONJ *) ("(אמר)"r verb infc) - ActuallyInf WITH (-1 ("(ל)"r prep)) ;

# Substantivized adjectives
SUBSTITUTE (adjv) (subs retag:adjv) (c) ;
SUBSTITUTE (adjv) (subs retag:adjv) (a) (1 BD) (-1* Noun + (c) BARRIER (*) - Det) ;
SUBSTITUTE (adjv) (subs retag:adjv) (adjv a) (1 (prn)) ;
# TODO: does this really apply to all gentillics?
SUBSTITUTE (adjv) (subs retag:adjv) (adjv gntl pl) ;
SUBSTITUTE (adjv) (subs retag:adjv) (adjv gntl) (-1* Noun + (c) BARRIER (*) - Det) ;
SUBSTITUTE (adjv) (subs retag:adjv) (adjv gntl) (NEGATE 0* (subs) - (retag:adjv gntl) OR (nmpr) BARRIER PB) ;

# Participles in smixut
SUBSTITUTE (verb) (subs retag:verb) Participle + NPish ;

# Adverbs tagged subs
SET AdvOk = (*) - (punct) - (prn ModS) - ("מאד" padv) ;
SUBSTITUTE (subs) (advb retag:subs) (padv) - (NP) (-1* BD BARRIER AdvOk) (1* BD BARRIER AdvOk) ;

# Determiner on clause participles
SUBSTITUTE (art) (conj retag:art) (CP Rela) ;

# Prepositions tagged as nouns
SUBSTITUTE (subs) (conj retag:subs) ConjAbleSubsPrep + (CP) ;
SUBSTITUTE (subs) (advb retag:subs) ("אחר" subs ppre AdvP) (1 BD) ;
SUBSTITUTE (subs) (prep retag:subs) SubsToPrep + (subs ppre) ;
SUBSTITUTE (subs) (prep retag:subs) (ppre CP Conj)
    IF (NOT 0 SubsOverride)
       (NEGATE 0 ("טרם" subs) LINK -1 ("ב" prep))
       (NEGATE 0 ("בלי" subs CP) LINK -1* ("על" prep) BARRIER PB) ;

SUBSTITUTE (prep) (conj retag:prep) ConjAblePrep + (CP)
    IF (NOT 1 ("אשׁר" conj)) ;

# yesh and ein
SUBSTITUTE (subs) (verb retag:subs ClauseRoot) (nmcp) ;

# interrogative adverbs
SUBSTITUTE (inrg) (advb retag:inrg) ItgAdv ;

####################
# Lookup table overrides
####################

LIST ChildOverrides = (Genesis w45) ;
LIST ParentOverrides = (Genesis w41) ;

SETPARENT ChildOverrides TO (0* ParentOverrides) ;

LIST ObjOverrides = (Genesis w45) ;

MAP @obj ObjOverrides ;

####################
# Consistent labels
####################

MAP @det Det ;
MAP @mark SCONJ + (conj CP) ;
MAP @mark ("ה" inrg) ;
MAP @obj ("את") + Pr IF (1 RelMark) ;
MAP @obl Pr IF (1 RelMark) ;
MAP @case Pr ;
MAP @cc (conj) - (Rela) ;
MAP @advmod ADV - (role:P) ;
MAP @advmod ("למה" inrg) ;
MAP @discourse (intj) ; # TODO: is there a better way to handle הנה?

####################
# Pronoun suffixes
####################

SUBSTITUTE ("prn") ("אני") ("prn" prn p1 sg) ;
SUBSTITUTE ("prn") ("אנחנו") ("prn" prn p1 pl) ;
SUBSTITUTE ("prn") ("אתה") ("prn" prn p2 m sg) ;
SUBSTITUTE ("prn") ("את") ("prn" prn p2 f sg) ;
SUBSTITUTE ("prn") ("אתם") ("prn" prn p2 m pl) ;
SUBSTITUTE ("prn") ("אתן") ("prn" prn p2 f pl) ;
SUBSTITUTE ("prn") ("הוא") ("prn" prn p3 m sg) ;
SUBSTITUTE ("prn") ("היא") ("prn" prn p3 f sg) ;
SUBSTITUTE ("prn") ("הם") ("prn" prn p3 m pl) ;
SUBSTITUTE ("prn") ("הן") ("prn" prn p3 f pl) ;

SETPARENT ("ו" conj) - (_) (pl (*)) TO (sllr (*)) ;

LIST FinCL = /((S|V|O2?|PP?|ADV)-)+(S|V|O2?|PP?|ADV)/r ;
LIST FinCLPP = /((S|V|O2?|PP?|ADV)-)+PP/r ;
LIST EllpCL = /^((S|O2?|PP|ADV)-)+(S|O2?|PP|ADV)$/r ;
LIST VerbCL = /^V-|-V-|-V$/r ;

LIST ConjSeq = /Conj\\d.*/r ;

MAP @conj ConjSeq IF (c (conj)) ;

MAP @conj $$Function IF (c ("ו" conj)) (p $$Function) ;
MAP @conj (_) IF (c ("ו" conj)) (p (_)) ;

####################
# By role
####################

SECTION

MAP @orphan EllpCL ;

WITH (role:ADV) {
  MAP @obj (Objc) IF (NEGATE s (role:O)) ;
  MAP @obl (NP rela:Attr) OR (NP Time) OR (adjv ordn) ;
  MAP @xcomp (NP) + VerbCL ;
  MAP @obl:npmod (infa) ;
  MAP @obl (subs) OR (nmpr) ;
  MAP @advmod (*) ;
} ;

WITH (role:Appos) {
  MAP @amod (adjv) ;
  MAP @acl:relcl (verb ptca) ;
  MAP @appos (*) ;
} ;

WITH (role:O) {
  MAP @ccomp (verb) - Participle ;
  MAP @obj (*) IF (NEGATE c (vbcp)) ;
} ;

MAP @xcomp (role:O2) ;

WITH (role:PP) {
  MAP @mark (SCONJ) ;
  MAP @advcl (verb infc) IF (c ("ל" prep)) ;
  MAP @nmod (*) IF (p (subs retag:verb)) ;
  MAP @obl (*) IF (NEGATE c (vbcp)) ;
} ;

WITH (role:S) {
  MAP @nsubj:outer (S-P) IF (p (role:V)) ;
  MAP @nsubj (*) ;
} ;

WITH (role:V) OR (role:P) {
  MAP @cop (vbcp) + FinCL ;
  MAP @acl:relcl (verb ptca Np-Appos) ;
  SETPARENT @acl:relcl TO (p (QuanNP @compound:smixut) LINK p (*)) ;
} ;

####################
# Simple rules
####################

SECTION

LIST AclRules = NpRelp PpRelp ;
LIST ApposRules = NpNpNp NP3NP ;
LIST FlatRules = NumpNump ;
LIST NmodRules = 2Pp NpPp NumpPp ;
LIST NummodRules = NpNump NumpNP ;

MAP @acl:relcl AclRules ;
MAP @appos ApposRules ;
MAP @flat FlatRules ;
MAP @nmod NmodRules ;
MAP @nummod NummodRules ;

####################
# Other rules
####################

SECTION

WITH (NpAdjp) {
  MAP @det (prde) OR (ppde) ;
  MAP @acl:relcl (verb) ;
  MAP @amod (*) - (verb) ;
} ;

MAP @appos (prde NPofNP) ;
MAP @nmod:poss (prn NPofNP) OR (prn PrepNp+NomPrep) ;
LIST CompoundAble = (NP NPofNP) (PP NPofNP) QuanNP (subs NPofNP) ;
MAP @compound:smixut CompoundAble IF (p (c)) ;
MAP @advcl (verb infc ClCl) IF (p (verb) - (infc)) ;
MAP @vocative (Voct ClCl) OR (Voct ClCl2) IF (p (_) OR (role:P) OR (role:V)) ;
MAP @acl:relcl (verb infc NpInf) ; # TODO: maybe actually plain @acl?

MAP @appos (NPofNP) ;

MAP @csubj FinCL + (rela:Subj) ;

WITH (AdjpPp) {
  MAP @obl (*) IF (p (*) LINK c @cop) ;
  MAP @obl (*) IF (p (verb)) ;
  MAP @obl (*) IF (p @conj LINK p (verb)) ;
  MAP @nmod (*) ;
} ;

LIST ListRules = 2PpaPp AdjpAdjpandAdjp AdjpandAdjpAdjp CLaCL
	 		     2CLaCL 2Np 2NpaNpaNp 2Pp 3NpaNp AdjpaAdjp aNpaNp aNpaNpaNp
				 CLaCL CLandCL2 ClCl Conj3CL Conj4Np Conj4Pp Conj5Np
				 NP3NP NpaNp NPaNPaNPNPaNP NPaNPNP NPaNPNPaNP NpNpNp
				 NPNPNPaNPaNP NpNpNpNp NpPp NumpAndNump
				 PPandPP PpaPpPpaPp Relp3Relp PpPp ;
MAP @conj ListRules ;

LIST XcompInf = "יסף" "יכל" "חפץ" "מהר" "אמץ" "חדל" "כלה" "ירא" "מלא" "חלל" "קרב" "יאל" "לאה" "צוה" "נתן" ;
WITH (verb infc) + FinCLPP IF (c (prep PrepCL)) {
  MAP @xcomp (*) IF (p XcompInf) ;
  MAP @advcl (*) ;
} ;

SETPARENT (@case PpPp) TO (p @case LINK p (*)) ;

SECTION

SETPARENT (*) TO (p ("ו" conj) LINK p (*)) ;

WITH @ccomp IF (sl @ccomp) (c @cc) {
  SETPARENT (*) TO (jC1 (*)) ;
  SUBSTITUTE @ccomp @conj (*) ;
} ;

WITH (subs ppre c) IF (p @obl) {
  MAP @obl (*) ;
  SUBSTITUTE @obl @compound:smixut (*) IF (jC1A (*)) ;
  SETPARENT (*) TO (jC1 (*) LINK p (*)) ;
  SETCHILD (*) TO (jC1 (*)) ;
} ;

SETPARENT @cc TO (p @case LINK p (*)) ;

SETPARENT @appos OR @nmod TO (p (QuanNP) LINK p (*)) ;

SETPARENT SAFE (SCONJ) TO (1* @ccomp) ;
MAP @mark (SCONJ) ;

SETPARENT @nsubj TO (p @discourse LINK p (*)) ;

WITH (nmpr) IF (-1 (subs c)) (NOT p (*)) {
	SETPARENT (*) TO (-1 (*)) ;
	MAP @compound:smixut (*) ;
} ;

####################
# Clause Connections
####################

SECTION

LIST TextLevel = <txt:0> <txt:1> <txt:2> ;

SET NonPredAdv = (advb) - (role:P) ;
SET Top = (_) - (conj) - @discourse - NonPredAdv ;

SETPARENT (conj _) TO (1* (_)) ;

LIST NonAdvclConj = "ו" retag:art ;

WITH Top + (/^<txt:\(\\d+\)>$/r)
  IF (-1* Top + (VSTR:<txt=$1>) BARRIER Top) (c (conj) - NonAdvclConj) {
    SETPARENT (*) TO (jC1 (*)) ;
	MAP @advcl (*) ;
} ;

WITH Top + (/^<txt:\(\\d+\)>$/r)
  IF (-1*O (>>>) OR Top + (VSTR:<txt<$1>) LINK 1* Top + (VSTR:<txt=$1>))
     (NOT p (*)) {
    SETPARENT (*) TO (jC1 (*)) ;
    MAP @conj (*) IF (c (conj)) ;
	SETPARENT (verb infc) (c (prep)) TO (p (*) LINK c (@xcomp verb infc)) ;
} ;

LIST SpeakingVerb = quot "ברך" "צוה" "נגד" "דבר" "ענה" "שׁמע" ;

WITH Top + (/^<txt:\(\\d+\)>$/r)
  IF (-1* Top + (VSTR:<txt<$1>) BARRIER Top) {
    SETPARENT (*) TO (jC1 (*)) ;
    MAP @ccomp (*) IF (jC1 SpeakingVerb) ;
	MAP @xcomp (verb infc) IF (jC1 XcompInf) (c (prep)) ;
} ;

SECTION

SETPARENT (_) (p (>>>)) TO (slll (*)) ;

SECTION

MAP @ccomp (rela:Objc) ;

SETPARENT (@ccomp <txt:1>) TO (srrl (_ <txt:0>) - (conj)) ;

SETPARENT (_ <txt:1>) (p (<txt:0>)) TO (slll (_ <txt:1>) - (conj)) ;

AFTER-SECTIONS

MAP @root (*) ;

WITH @root IF (p (*) LINK p (*)) {
  SUBSTITUTE @root @conj (*) IF (c @cc) ;
  SUBSTITUTE @root @parataxis (*) ;
} ;

REMCOHORT (pb) ;
REMCOHORT (cb) ;
REMCOHORT (sb) ;
REMCOHORT (svb) ;
#REMCOHORT (excb) ;

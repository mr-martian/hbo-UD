DELIMITERS = "<svb>" ;

####################
# Boundaries
####################

LIST PB = pb ;
LIST CB = cb ;
LIST SB = sb ;
LIST SVB = svb ;
LIST BD = pb cb sb svb ;

####################
# POS
####################

LIST Det = art ;
LIST Pr = prep ;
LIST Noun = subs nmpr (verb ptcp) (verb ptca) ;
LIST RelMark = ("אשׁר" conj @mark) ("ה" conj CP Rela) ("שׁ" conj) ;
LIST PRON = prde prps prn ;
LIST ADV = advb nega ;
LIST SCONJ = "כי" "פן" "אשׁר" "אם" "שׁ" "ה" "לולא" "לו" (conj retag:prep) (conj retag:subs ppre) ;

####################
# Subcategories
####################

LIST Participle = ptca ptcp ;

####################
# Text-Fabric labels
####################

# Complete

LIST Function = Adju Cmpl Conj EPPr ExsS Exst Frnt Intj IntS Loca Modi ModS NCop NCoS Nega Objc PrAd PrcS PreC Pred PreO PreS PtcO Ques Rela Subj Supp Time Unkn Voct ;
LIST ClauseType = AjCl CPen Defc Ellp InfA InfC MSyn NmCl Ptcp Reop Unkn Voct Way0 WayX WIm0 WImX WQt0 WQtX WxI0 WXIm WxIX WxQ0 WXQt WxQX WxY0 WXYq WxYX WYq0 WYqX xIm0 XImp xImX XPos xQt0 XQtl xQtX xYq0 XYqt xYqX ZIm0 ZImX ZQt0 ZQtX ZYq0 ZYqX ; # feature typ
LIST AGen = m f ;

####################
# Helper labels
####################

LIST HasConj = HasConj ;

LIST PPHead = PPHead ;

LIST NQ = NQ ;
LIST QuoteLevel = NQ Q QQ ;

####################
# Word lists
####################

LIST ConjAblePrep = "כמו" "למען" "עד" ;
LIST ConjAbleSubsPrep = "בלת" ;
LIST SubsToPrep = "אחר" "בין" "אצל" "בעד" ;

LIST Masculine = "מכיר" "אשׁר" "בצע" "בקר" ;
# TODO: BDB says sometimes masculine
LIST FeminineMixed = "ארץ" "יד" "צאן" "אשׁ" "גן" "גפן" ;
# TODO: BDB says sometimes feminine
LIST MasculineMixed = "לחם" "דרך" "אור" "אות" "ארון" "גמל" ;

####################
# Special-case words
####################

LIST PPHeadOverride = (Ruth w575) (Ruth w580) ;

LIST DontConjTo = (Genesis w4993) ;
LIST HasConjOverride = (Genesis w4584) ;

####################
# Universal Dependencies
####################

LIST @root = @root ;     # The root of the sentence, often a finite verb
LIST @nsubj = @nsubj ;   # The nominal subject of the sentence
LIST @nsubj:outer = @nsubj:outer ;
LIST @amod = @amod ;       #
LIST @advmod = @advmod ; # An adverbial modifier
LIST @case = @case ;     # The relation of an adposition to its head
LIST @acl:relcl = @acl:relcl ;       # A clause which modifies a nominal
LIST @nmod = @nmod ;     # Nominal modifier
LIST @dobj = @dobj ;     # The direct object of the sentence
LIST @punct = @punct ;   # Any punctuation
LIST @cop = @cop ;       #
LIST @nmod:poss = @nmod:poss ;
LIST @obl = @obl ;
LIST @obl:npmod = @obl:npmod ;
LIST @obj = @obj ;
LIST @advcl = @advcl ;
LIST @aux = @aux ;
LIST @parataxis = @parataxis ;
LIST @det = @det ;
LIST @csubj = @csubj ;
LIST @nummod = @nummod ;
LIST @compound = @compound ;
LIST @compound:smixut = @compound:smixut ;
LIST @cc = @cc ;
LIST @conj = @conj ;
LIST @ccomp = @ccomp ;
LIST @mark = @mark ;
LIST @discourse = @discourse ;
LIST @vocative = @vocative ;
LIST @appos = @appos ;
LIST @dislocated = @dislocated ;
LIST @xcomp = @xcomp ;
LIST @flat = @flat ;
LIST @flat:name = @flat:name ;
LIST @orphan = @orphan ;
LIST @fixed = @fixed ;
LIST @dep = @dep ;       # Any remaining dependency

####################
# Unsorted lists
####################

LIST NPish = NP PP ;
LIST SubsOverride = (Genesis w20500) (Ruth w1233) (Genesis w13703) (Genesis w14216) ; # actually nouns, don't retag as prep
LIST ItgAdv = "מתי" "איפה" "איך" "אן" "מדוע" ;
LIST VoctNonCROverride = (Genesis s81570) (Genesis s81725) (Genesis s85145) (Genesis s82809) (Genesis w9312) ;
LIST ActuallyInf = (Genesis w13111) ;

########################################
# INDIVIDUAL WORDS
########################################

BEFORE-SECTIONS

####################
# Hide Punctuation
####################

MAP @punct (punct) ;
REMCOHORT IGNORED (punct) ;

####################
# Corrections
####################

LIST MislabeledAsNarrative = w1404 w1405 w1423 w1424 w14251 w14252 w14253 (Genesis s80885) (Ruth s5672) (Ruth s5673) (Genesis s83562) (Ruth s5647) (Ruth s5650) (Ruth s5651) (Genesis s85072) (Genesis s83308) (Genesis s83309) (Genesis s84608) (Genesis s83116) (Genesis s83082) (Genesis s81615) (Genesis s81616) (Genesis s81881) ;
SUBSTITUTE (N) (Q) MislabeledAsNarrative ;

LIST UnknownShouldBeNarrative = (Genesis w2137) ;
SUBSTITUTE (?) (N) UnknownShouldBeNarrative ;

SUBSTITUTE (infc) (infa) (Ruth w797) ;

ADD (m) Noun - AGen IF (0 Masculine OR MasculineMixed) ;
ADD (f) Noun - AGen IF (0 FeminineMixed) ;

####################
# Nested Quotations
####################

LIST DoubleQuoteGen = s80831 s80834 s80836 s80837 s80886 s81424 s81443 s81543 s81737 s81766 s81767 s81785 s81829 s81933 s81999 s82001 s82028 s82037 s82159 s82295 s82315 s82367 s82380 s82382 s82410 s82412 s82413 s82446 s82450 s82599 s82626 s82629 s82776 s82946 s83252 s83255 s83285 s83303 s83318 s83319 s83327 s83431 s83432 s83449 s83459 s83470 s83474 s83479 s83480 s83482 s83510 s83626 s83628 s83906 s83919 s84071 s84283 s84313 s84382 s84435 s84464 s84492 s84537 s84545 s84551 s84552 s84555 s84638 s84673 s84681 s84708 s84709 s84719 s84722 s84765 s84804 s84805 s84828 s84934 s84941 s84943 s85134 s85229 s85230 s85261 s85262
c29820 ; # TODO: handle rela:Objc properly
LIST DoubleQuoteRuth = s5648 s5649 s5720 s5825 ;
SET DoubleQuote = (Genesis) + DoubleQuoteGen OR (Ruth) + DoubleQuoteRuth ;
SUBSTITUTE (Q) (QQ) DoubleQuote ;

ADD NQ (*) - QuoteLevel ;

####################
# MWE names
####################

SUBSTITUTE (nmpr a) (subs c m retag:nmpr) ("בית" wp1) ;
SUBSTITUTE (nmpr a) (subs c m retag:nmpr) ("בן" wp1) ;
SUBSTITUTE (nmpr) (subs retag:nmpr m) ("אל" wp2) ;
SUBSTITUTE (nmpr) (subs retag:nmpr) ("לחם" wp2) ;
SUBSTITUTE (nmpr) (subs retag:nmpr) ("צדק" wp2) ;
SUBSTITUTE ("מלכי" nmpr) ("מלך" subs c retag:nmpr) (wp1 Genesis w6557) ;
SUBSTITUTE ("עמי" nmpr) ("עם" subs retag:nmpr has_prn) ("עמי" wp2) ;
SUBSTITUTE ("אוני" nmpr) ("און" subs retag:nmpr has_prn) ("אוני" wp2) ;
ADDCOHORT ("<prn>" "אני" prn p1 sg) AFTER ("עם" subs retag:nmpr) ;
ADDCOHORT ("<prn>" "אני" prn p1 sg) AFTER ("און" subs retag:nmpr) ;

MAP @flat:name (nmpr wp2) IF (-1 (nmpr wp1)) ;
SETPARENT @flat:name TO (-1* (nmpr wp1)) ;

####################
# MWE conjunctions
####################

SETPARENT ("אם" conj) OR ("כי" conj) TO (-1 ("עד" prep)) ;
MAP @fixed ("אם" conj) OR ("כי" conj) IF (-1 ("עד" prep)) ;
SUBSTITUTE (prep) (conj retag:prep) ("עד") IF (c @fixed) ;

####################
# Retagging
####################

# quotation
MERGECOHORTS ("<le'emor>" "$2$1"v SCONJ *) ("(אמר)"r verb infc) - ActuallyInf WITH (-1 ("(ל)"r prep)) ;

# Substantivized adjectives
SUBSTITUTE (adjv) (subs retag:adjv) (c) ;
SUBSTITUTE (adjv) (subs retag:adjv) (a) (1 BD) (-1* Noun + (c) BARRIER (*) - Det) ;
SUBSTITUTE (adjv) (subs retag:adjv) (adjv a) (1 (prn)) ;
# TODO: does this really apply to all gentillics?
SUBSTITUTE (adjv) (subs retag:adjv) (adjv gntl pl) ;
SUBSTITUTE (adjv) (subs retag:adjv) (adjv gntl) (-1* Noun + (c) BARRIER (*) - Det) ;
SUBSTITUTE (adjv) (subs retag:adjv) (adjv gntl) (NEGATE 0* (subs) - (retag:adjv gntl) OR (nmpr) BARRIER PB) ;

# Participles in smixut
SUBSTITUTE (verb) (subs retag:verb) Participle + NPish ;

# Adverbs tagged subs
SET AdvOk = (*) - (punct) - (prn ModS) - ("מאד" padv) ;
SUBSTITUTE (subs) (advb retag:subs) (padv) - (NP) (-1* BD BARRIER AdvOk) (1* BD BARRIER AdvOk) ;

# Determiner on clause participles
SUBSTITUTE (art) (conj retag:art) (CP Rela) ;

# Prepositions tagged as nouns
SUBSTITUTE (subs) (conj retag:subs) ConjAbleSubsPrep + (CP) ;
SUBSTITUTE (subs) (advb retag:subs) ("אחר" subs ppre AdvP) (1 BD) ;
SUBSTITUTE (subs) (prep retag:subs) SubsToPrep + (subs ppre) ;
SUBSTITUTE (subs) (prep retag:subs) (ppre CP Conj)
    IF (NOT 0 SubsOverride)
       (NEGATE 0 ("טרם" subs) LINK -1 ("ב" prep))
       (NEGATE 0 ("בלי" subs CP) LINK -1* ("על" prep) BARRIER PB) ;

SUBSTITUTE (prep) (conj retag:prep) ConjAblePrep + (CP)
    IF (NOT 1 ("אשׁר" conj)) ;

# yesh and ein
SUBSTITUTE (subs) (verb retag:subs ClauseRoot) (nmcp) ;

# interrogative adverbs
SUBSTITUTE (inrg) (advb retag:inrg) ItgAdv ;

####################
# Helper labels
####################

ADD (NonAcc) ("את" prep Cmpl) + $$ClauseType - (NonAcc)
    IF (0* ("את" prep Objc) + $$ClauseType BARRIER SB) ;
ADD PPHead PPHeadOverride - PPHead ;

SUBSTITUTE (ClausePhraseRoot) (WhyAreVocativesTheirOwnClause) VoctNonCROverride ;
SUBSTITUTE (ClausePhraseRoot) (WhyAreVocativesTheirOwnClause) (Voct) IF (0* (ClausePhraseRoot) - (Voct) BARRIER SB) ;

ADD (ClausePhraseRoot) ("חלילה" intj) - (ClausePhraseRoot) ;
SUBSTITUTE (ClausePhraseRoot) (InterjectionOverride) (*) - (intj)
    IF (0* ("חלילה" intj) BARRIER CB) ;

ADD (skipconj) DontConjTo - (skipconj) ;
ADD HasConj HasConjOverride - HasConj ;

####################
# Consistent labels
####################

MAP @punct (punct) ;
MAP @det Det ;
MAP @mark SCONJ + (conj CP) ;
MAP @mark ("ה" inrg) ;
MAP @obj ("את") + Pr IF (1 RelMark) ;
MAP @obl Pr IF (1 RelMark) ;
MAP @case Pr ;
MAP @cc (conj) - (Rela) ;
MAP @advmod ADV - (ClausePhraseRoot) - PPHead ;
MAP @advmod ADV + (MSyn) ;
MAP @advmod ADV + (ClausePhraseRoot) IF (0* PRON BARRIER BD) ;
MAP @advmod ("למה" inrg) ;
MAP @discourse (intj) - (ClausePhraseRoot) ; # TODO: is there a better way to handle הנה?

####################
# Pronoun suffixes
####################

SUBSTITUTE ("prn") ("אני") ("prn" prn p1 sg) ;
SUBSTITUTE ("prn") ("אנחנו") ("prn" prn p1 pl) ;
SUBSTITUTE ("prn") ("אתה") ("prn" prn p2 m sg) ;
SUBSTITUTE ("prn") ("את") ("prn" prn p2 f sg) ;
SUBSTITUTE ("prn") ("אתם") ("prn" prn p2 m pl) ;
SUBSTITUTE ("prn") ("אתן") ("prn" prn p2 f pl) ;
SUBSTITUTE ("prn") ("הוא") ("prn" prn p3 m sg) ;
SUBSTITUTE ("prn") ("היא") ("prn" prn p3 f sg) ;
SUBSTITUTE ("prn") ("הם") ("prn" prn p3 m pl) ;
SUBSTITUTE ("prn") ("הן") ("prn" prn p3 f pl) ;

#SETPARENT (conj @cc) (pr (*)) TO (sllr (*)) ;

WITH ("ו" conj) IF (cllr (*)) (NEGATE cl (*)) {
  MAP REPEAT @conj (*) IF (cA (*) - (conj)) ;
  SETPARENT REPEAT (*) (cA (*)) TO (jC1 (*)) ;
  SETPARENT _C1_ TO (p (*) LINK p (*)) ;
  SETPARENT (*) TO (jC1 (*)) ;
} ;

WITH ("ו" conj) IF (pl (*)) (sllr (*)) {
  SETPARENT (*) TO (sllr (*)) ;
  MAP @cc (*) ;
  MAP @conj (*) IF (pA (*)) ;
} ;

WITH ("היה" vbcp) IF (c (PreC) - @conj - (_)) {
  MAP @cop (*) ;
  SETPARENT _C1_ TO (p (*) LINK p (*)) ;
  SETPARENT (*) TO (jC1 (*)) ;
} ;

SETPARENT (*) TO (p ("היה" vbcp @cop) LINK p (PreC)) ;

WITH (2Pp) IF (p (prep)) {
  SETPARENT (*) TO (jC1 (*) LINK p (*)) ;
  SETCHILD (*) TO (jC1 (*)) ;
  SETCHILD REPEAT (*) TO (jC1 (*) LINK c (*)) ;
  MAP @acl:relcl (*) IF (jC1 (NpRelp)) ;
} ;

MAP @ccomp (txt:1) IF (p (txt:0)) ;

#SETPARENT (*) (p ("היה" vbcp)) TO (p (*) LINK p (*)) ;

LIST FinCL = /((S|V|O2?|PP?)-)+(S|V|O2?|PP?)/r ;
LIST FinCLPP = /((S|V|O2?|PP?)-)+PP/r ;

LIST ConjSeq = /Conj\\d.*/r ;

MAP @conj ConjSeq IF (c (conj)) ;

MAP @conj $$Function IF (c ("ו" conj)) (p $$Function) ;
MAP @conj (_) IF (c ("ו" conj)) (p (_)) ;

MAP @nmod:poss (prn NPofNP) ;
MAP @compound:smixut (NP NPofNP) OR (PP NPofNP) OR (QuanNP) IF (p (c)) ;
MAP @appos (NPofNP) ;
MAP @nsubj (role:S) ;
MAP @ccomp (verb role:O) - Participle ;
MAP @obj (role:O) ;
MAP @obj (role:Np Objc) + FinCL ;
MAP @mark (SCONJ role:PP) ;
MAP @obl (role:PP) ;
MAP @obj (Objc role:ADV) IF (NEGATE s (role:O)) ;
MAP @xcomp (NP role:ADV) ;
MAP @obl:npmod (infa role:ADV) ;
MAP @advmod (role:ADV) ;
MAP @obl (role:O2) ;
MAP @nummod (NpNump) OR (NumpNP) ;
MAP @det (prde NpAdjp) ;
MAP @amod (NpAdjp) - (verb) ;
MAP @acl:relcl (NpRelp) ;
MAP @nmod (2Pp) OR (NpPp) ;
MAP @acl:relcl (verb ptca Np-Appos) ;
MAP @appos (NpNpNp) OR (NP3NP) OR (role:Appos) ;
MAP @acl:relcl (verb NpAdjp) ;

LIST ListRules = 2PpaPp AdjpAdjpandAdjp AdjpandAdjpAdjp CLaCL
	 		     2CLaCL 2Np 2NpaNpaNp 2Pp 3NpaNp AdjpaAdjp aNpaNp aNpaNpaNp
				 CLaCL CLandCL2 ClCl Conj3CL Conj4Np Conj4Pp Conj5Np
				 NP3NP NpaNp NPaNPaNPNPaNP NPaNPNP NPaNPNPaNP NpNpNp
				 NPNPNPaNPaNP NpNpNpNp NpPp NumpAndNump NumpNump
				 PPandPP PpaPpPpaPp Relp3Relp ;
MAP @conj ListRules ;

LIST XcompInf = "יסף" "יכל" "חפץ" "מהר" "אמץ" "חדל" "כלה" "ירא" "מלא" "חלל" "קרב" "יאל" "לאה" "צוה" "נתן" ;
MAP @xcomp (verb infc) + FinCLPP IF (p XcompInf) (c (prep PrepCL)) ;
MAP @advcl (verb infc) + FinCLPP IF (c (prep PrepCL)) ;

SUBSTITUTE @nsubj @obl (*) IF (sl (V-S-PP-PP @nsubj)) ;
SUBSTITUTE @obj @obl (PP) IF (sl FinCLPP + @obj) ;
SUBSTITUTE @obj @xcomp (adjv) IF (s FinCL + @obj) ;

MAP @obj (prn V-O) IF (p (verb)) ;

SECTION

SETPARENT (*) TO (p ("ו" conj) LINK p (*)) ;

WITH @ccomp IF (sl @ccomp) (c @cc) {
  SETPARENT (*) TO (jC1 (*)) ;
  SUBSTITUTE @ccomp @conj (*) ;
} ;

#WITH (PPandPP) OR (PpPp) - (prep) IF (pr (@obl)) {
#  MAP @obl (*) ;
#  SETPARENT UNSAFE (*) TO (jC1 (*) LINK p (*)) ;
#  SETCHILD (*) TO (jC1 (*)) ;
#  SUBSTITUTE @obl @conj _C1_ ;
#} ;

WITH (subs ppre c) IF (p @obl) {
  MAP @obl (*) ;
  SUBSTITUTE @obl @compound:smixut (*) IF (jC1A (*)) ;
  SETPARENT (*) TO (jC1 (*) LINK p (*)) ;
  SETCHILD (*) TO (jC1 (*)) ;
} ;

LIST PPConj = Conj3Pp ;

WITH PPConj IF (p @obl) (NEGATE ls (*)) {
  MAP @obl (*) ;
  MAP REPEAT @conj (*) IF (sA PPConj - (conj)) ;
  SETCHILD (*) TO (s @conj) ; # TODO: REPEAT does weird things?
  SETPARENT (*) TO (p (*) LINK p (*)) ;
  SUBSTITUTE @obl @conj _C1_ ;
  SETCHILD (*) TO (jC1 (*)) ;
} ;

SETPARENT @cc TO (p @case LINK p (*)) ;

SETPARENT @appos OR @nmod TO (p (QuanNP) LINK p (*)) ;

SETPARENT SAFE (SCONJ) TO (1* @ccomp) ;
MAP @mark (SCONJ) ;

SETPARENT @nsubj TO (p @discourse LINK p (*)) ;

SECTION

MAP @ccomp (rela:Objc) ;

SETPARENT (@ccomp txt:1) TO (srrl (_ txt:0) - (conj)) ;

AFTER-SECTIONS

MAP @root (*) ;

SUBSTITUTE @root @parataxis (*) IF (p @root) ;

REMCOHORT (pb) ;
REMCOHORT (cb) ;
REMCOHORT (sb) ;
REMCOHORT (svb) ;
#REMCOHORT (excb) ;
